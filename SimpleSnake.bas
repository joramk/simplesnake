10 REM ---- SIMPLE SNAKE BY MAR3K ----
11 COLOR 7,8: CLS: POKE $30D,27: POKE $30E,25: SYS $FFF0
12 PRINT "SIMPLE SNAKE (C)2021 MAR3K": POKE $30D,29: POKE $30E,27: SYS $FFF0
13 PRINT "PRESS ANY KEY TO START": GOSUB 3500: DIM P%(255)

20 REM ---- INIT GAME SCREEN ----
30 SCREEN$02: COLOR 1, 0: CLS: X = RND(-TI)
40 FOR X = 0 TO 79
50 VPOKE 0, X*2 + 1, 12*16 + 1: VPOKE 0, 59*256 + X*2 + 1, 12*16 + 1
60 NEXT X
70 FOR Y = 0 TO 59
80 VPOKE 0, Y*256 + 1, 12*16 + 1: VPOKE 0, Y*256 + 159, 12*16 + 1
90 NEXT Y

100 REM ---- INIT SNAKE DRAWING ----
110 XS% = INT(RND(1) * 64) + 8: YS% = INT(RND(1) * 42) + 8
120 REM OC = 193: FOR I = 0 TO 32: GOSUB 2000: NEXT I
130 P%(0) = XS%*2 + YS%*256 + 1
140 SC% = 0: DI% = 2: SI% = 3: OC% = 33: GOSUB 2000
150 GOSUB 500

199 REM ---- MAIN ROUTINE ----
200 J = JOY(1)
210 IF J AND 8 THEN DI% =-256 :REM UP
220 IF J AND 4 THEN DI% = 256 :REM DOWN
230 IF J AND 2 THEN DI% =  -2 :REM LEFT
240 IF J AND 1 THEN DI% =   2 :REM RIGHT

249 REM ---- REMOVE THE SNAKES TAIL ----
250 IF P%(SI%) <> 0 THEN VPOKE 0, P%(SI%), 1

259 REM ---- MAYBE SHOW A RANDOM GOODIE ----
260 IF INT(RND(1) * 100) = 1 THEN OC% = 113: GOSUB 2000

299 REM ---- SHIFT SNAKE ARRAY ----
300 FOR I = SI% TO 1 STEP -1: P%(I) = P%(I-1): NEXT I
330 P%(0) = P%(0) + DI%

339 REM ---- DETECT COLLISION THEN DRAW SNAKE ----
340 IF VPEEK(0, P%(0)) <> 1 THEN GOSUB 1000
350 IF P%(0) <> 0 THEN VPOKE 0, P%(0), 5 * 16 + 1

399 REM ---- SLOW DOWN MOVEMENT ----
400 T0 = TI
410 IF TI-T0 <= 18 - SI%/2 THEN GOTO 410
420 GOTO 200: REM MAIN LOOP

499 REM ---- PRINT SCORES ----
500 POKE $30D,1: POKE $30E,67: SYS $FFF0
510 PRINT "SCORE:";: PRINT SC%
520 POKE $30D,1: POKE $30E,4: SYS $FFF0
530 PRINT "HIGH SCORE:";: PRINT HS%: RETURN

999 REM ---- COLLISION HANDLING ----
1000 IF VPEEK(0, P%(0)) = 193 THEN GOTO  5000: REM BORDER
1005 IF VPEEK(0, P%(0)) =  81 THEN GOTO  5000: REM SNAKE
1010 IF VPEEK(0, P%(0)) =  33 THEN GOSUB 4000: REM BERRY
1015 IF VPEEK(0, P%(0)) = 113 THEN GOSUB 4500: REM GOODIE
1020 RETURN

1999 REM ---- DRAW A RANDOM DOT ----
2000 XE% = INT(RND(1) * 76) + 2: YE% = INT(RND(1) * 56) + 2
2005 PE% = XE%*2 + YE%*256 + 1
2010 IF XS% = XE% OR YS% = YE% OR VPEEK(0, PE%) <> 1 THEN GOTO 2000
2015 IF OC% = 113 AND GP% <> 0 THEN VPOKE 0, GP%, 0
2020 IF OC%<> 113 OR INT(RND(0)*4)<>1 THEN VPOKE 0, PE%, OC%
2025 IF OC% = 113 THEN GP% = PE%
2030 RETURN

3499 REM ---- GET ANY KEY PRESS ----
3500 GET A$: IF A$="" THEN 3500
3510 A%=ASC(A$): IF (A%<32 OR A%>127) AND A%<>13 THEN 3500
3520 RETURN

3999 REM ---- CATCHED THE BERRY ----
4000 SI% = SI% + 3: SC% = SC% + 3: GOSUB 500
4010 REM OC% = 193: FOR I = 0 TO 8: GOSUB 2000:NEXT I
4020 OC% = 33: GOSUB 2000: RETURN

4499 REM ---- CATCHED THE GOODIE ----
4500 IF SI%>3 THEN VPOKE 0, P%(SI%), 0: SI%=SI%-1: SC%=SC%+1: GOSUB 500
4510 RETURN

4999 REM ---- GAME OVER ----
5000 COLOR 1,2: CLS: POKE $30D,27: POKE $30E,34: SYS $FFF0
5005 IF HS% < SC% THEN HS% = SC%: REM SAVE NEW HIGH SCORE
5010 PRINT "GAME OVER !": POKE $30D,29: POKE $30E,35: SYS $FFF0
5015 PRINT "SCORE:";: PRINT SC%: POKE $30D,31: POKE $30E,32: SYS $FFF0
5020 PRINT "HIGH SCORE:";: PRINT HS%: POKE $30D,33: POKE $30E,30: SYS $FFF0
5025 PRINT "PLAY ANOTHER ROUND? ";: GOSUB 3500: PRINT A$: IF A%=89 THEN GOTO 20
5030 COLOR 1,6: CLS
